import com.github.spotbugs.SpotBugsTask

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:5.3.0'
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.ben-manes.versions' version "${versionsPluginVersion}"
    id 'com.github.spotbugs' version "${spotbugsPluginVersion}"
    id 'org.springframework.boot' version "${springBootPluginVersion}"
    id "org.owasp.dependencycheck" version "${dependencyCheckPluginVersion}"
    id 'org.sonarqube' version "${sonarqubePluginVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementPluginVersion}"
    id 'project-report'
}

group = 'com.jrmcdonald'
version = "${majorVersion}.${minorVersion}.${patchVersion}"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    implementation "com.github.spotbugs:spotbugs-annotations:${spotbugsVersion}"
    implementation "net.sf.biweekly:biweekly:${biweeklyVersion}"
    implementation "org.springframework.cloud:spring-cloud-function-adapter-azure:3.0.2.RELEASE"
    implementation 'org.springframework.cloud:spring-cloud-starter-function-webflux'

    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"
    testImplementation "com.squareup.okhttp3:okhttp:${okhttpVersion}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${okhttpVersion}"
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'io.projectreactor:reactor-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

compileJava {
    options.compilerArgs << "-Werror"
    options.compilerArgs << "-Xlint:all"
    options.compilerArgs << "-Xlint:-try"
    options.compilerArgs << "-Xlint:-processing"
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "${jacocoToolVersion}"
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

dependencyCheck {
    format = 'ALL'
}

spotbugs {
    toolVersion = "${spotbugsToolVersion}"
    ignoreFailures = true // TODO: Remove and fix issues
}

tasks.withType(SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    spotbugsTest.enabled = false
}

plugins.withType(JacocoPlugin) {
    tasks["test"].finalizedBy 'jacocoTestReport'
}

htmlDependencyReport {
    projects = project.allprojects
}

dependencyReport {
    projects = project.allprojects
}

sonarqube {
    properties {
        property "sonar.projectKey", "jrmcdonald_ifsc-ical-generator"
        property "sonar.organization", "jrmcdonald"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }
}

check.dependsOn htmlDependencyReport
check.dependsOn dependencyReport
check.dependsOn dependencyCheckAnalyze
build.dependsOn dependencyUpdates
test.finalizedBy jacocoTestReport